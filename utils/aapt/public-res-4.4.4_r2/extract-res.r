REBOL [
	Title:   "Extract public resource id from Android's res XML files"
	Author:  "Qingtian Xie"
	File: 	 %extract-res.r
	Tabs:	 4
	Usage:	 {
		$rebol %public-res-4.4.4_r2/extract-res.r %public-res-4.4.4_r2
	}
	Rights:  "Copyright (C) 2014 Qingtian Xie. All rights reserved."
	License: {
		Distributed under the Boost Software License, Version 1.0.
		See https://github.com/dockimbel/Red/blob/master/BSL-License.txt
	}
]

fail: func [value][
	print value
	if system/options/args [quit/return 1]
	halt
]

absolute-path: func [src /local base-path][
	base-path: system/script/parent/path
	if slash <> first src [								;-- if relative path
		src: clean-path join base-path src				;-- add working dir path
	]
	src
]

load-dir: func [dirname /local result][
	unless any [
		all [
			#"%" = first dirname
			attempt [result: absolute-path load dirname]
			dir? result
		]
		all [
			attempt [result: absolute-path to-rebol-file dirname]
			dir? result
		]
	] [
		fail ["Invalid dirname:" dirname]
	]
	dirize result
]

parse-attr-values: func [blk [block!] /local attr name value values][
	values: copy []
	foreach node blk [
		if block? node [
			attr: node/2
			name: select attr "name"
			value: select attr "value"
			repend values [name value]
		]
	]
	values
]

parse-attrs: func [blk [block!] /local name format values attrs][
	attrs: copy []
	foreach [tag attr body] blk [
		if tag = "attr" [
			foreach [key value] attr [
				switch key [
					"name"	 [name: value]
					"format" [format: value]
				]
			]
			if body [
				format: 'bag
				values: parse-attr-values body
			]
			if format [repend attrs [name reduce [format values]]]
		]
		if body [
			foreach item body [
				if block? item [append attrs parse-attrs item]
			]
		]
	]
	attrs
]

parse-public: func [blk [block!] /local name id type publics][
	publics: copy []
	foreach [tag attr body] blk [
		if tag = "public" [
			foreach [key value] attr [
				switch key [
					"name" [name: value]
					"id"   [id: value]
					"type" [type: value]
				]
			]
			if id [repend publics [name id type]]
		]
		if body [
			foreach item body [
				if block? item [append publics parse-public item]
			]
		]
	]
	publics
]

main: has [
	args res-dir public-xml attrs-xml attrs-manifest-xml type-res res-id
	all-attrs all-publics attr format value blk
][
	print "Generating android-res.r..."
	args: any [
		system/options/args
		parse any [system/script/args ""] none
	]
	res-dir: load-dir args/1

	public-xml: join res-dir "public.xml"
	attrs-xml: join res-dir "attrs.xml"
	attrs-manifest-xml: join res-dir "attrs_manifest.xml"

	type-res: make block! 32
	append type-res [REBOL [
			Title:   "Convert Android's public.xml to REBOL block"
			File: 	 %android-res.r
			Note:	 "This file is generated by %extract-res.r"
			License: {
				Distributed under the Boost Software License, Version 1.0.
				See https://github.com/dockimbel/Red/blob/master/BSL-License.txt
			}
		]
	]

	all-attrs: parse-attrs parse-xml read attrs-xml
	append all-attrs parse-attrs parse-xml read attrs-manifest-xml
	all-attrs: to-hash all-attrs
	all-publics: parse-public parse-xml read public-xml

	foreach [name id type] all-publics [
		unless res-id: select type-res type [
			res-id: make hash! 500
			repend type-res [type res-id]
		]
		format: none
		value: none
		either type = "attr" [
			attr: select all-attrs name
			either attr [
				format: attr/1
				value: attr/2
			][
				print ["Can't find resource: " name]
			]
			blk: either value [[id format value]][[id format]]
			repend res-id [name reduce blk]
		][
			repend res-id [name id]
		]
	]

	print reduce ["Saving to" join what-dir %android-res.r]
	save %android-res.r type-res
	print "Done."
]

main